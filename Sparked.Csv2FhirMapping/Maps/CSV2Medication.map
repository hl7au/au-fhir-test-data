map "http://example.org/StructureMap/CSV2Medication" = "CSV2Medication"

uses "http://example.org/StructureDefinition/MedicationCSV" as source
uses "http://hl7.org/fhir/StructureDefinition/Medication" as target

group ToMedication(source src : MedicationCSV, target tgt : Medication) {

    // id
    src.id as s_id where length() > 0 -> tgt.id = s_id; 

    // meta.profile
    src -> tgt.meta = create('Meta') as meta then {
        src -> meta.profile = 'http://hl7.org.au/fhir/core/StructureDefinition/au-core-medication';
    };

    // code 1..1 MS
    src.code_coding1_code as s_coding1 where length() > 0 -> tgt.code as t_code then {
        s_coding1 where length() > 0 -> t_code then {
            src.code_coding1_system as coding1_system -> t_code.coding = c(coding1_system, s_coding1) as t_coding first then {
                src.code_coding1_display as s_display where length() > 0 -> t_coding.display = s_display;

                src.code_coding1_medicationType_code as s_medicationType_code where length() > 0 -> t_coding.extension as t_medication_type first then {
                    s_medicationType_code -> t_medication_type.url = "http://hl7.org.au/fhir/StructureDefinition/medication-type";
                    s_medicationType_code -> t_medication_type.value = c("http://terminology.hl7.org.au/CodeSystem/medication-type", s_medicationType_code) as t_coding then {
                        src.code_coding1_medicationType_display as s_medicationType_display where length() > 0 -> t_coding.display = s_medicationType_display;
                    };
                }; 
            };

            src.code_coding2_code as s_coding2 where length() > 0 -> t_code then {
                src.code_coding2_system as coding2_system -> t_code.coding = c(coding2_system, s_coding2) as t_coding first then {
                    src.code_coding2_display as s_display where length() > 0 -> t_coding.display = s_display;
                };
            };

            src.code_coding3_code as s_code where length() > 0 -> t_code then {
                src.code_coding3_system as s_system -> t_code.coding = c(s_system, s_code) as t_coding first then {
                    src.code_coding3_display as s_display where length() > 0 -> t_coding.display = s_display;
                };
            };

        };

        src.code_text as s_text where length() > 0 -> t_code.text = s_text;
    };

    // code coding missing
    src.code_coding1_code as s_coding1 where length() = 0 -> tgt.code as t_code then {
        // text only 
        src.code_text as s_text where length() > 0 -> t_code.text = s_text;
        // code missing
        src.code_text as s_text where length() = 0 -> t_code.coding = c('http://terminology.hl7.org/CodeSystem/data-absent-reason', 'unknown');
    };

    // form 0..1 
    src.form_coding1_code as s_coding1 where length() > 0 -> tgt.form as t_form then {
        s_coding1 -> t_form then {
            src.form_coding1_system as coding1_system -> t_form.coding = c(coding1_system, s_coding1) as t_coding first then {
                src.form_coding1_display as s_display where length() > 0 -> t_coding.display = s_display;
            };

            src.form_coding2_code as s_coding2 where length() > 0 -> t_form then {
                src.form_coding2_system as coding2_system -> t_form.coding = c(coding2_system, s_coding2) as t_coding first then {
                    src.form_coding2_display as s_display where length() > 0 -> t_coding.display = s_display;
                };
            };

            src.form_coding3_code as s_code where length() > 0 -> t_form then {
                src.form_coding3_system as s_system -> t_form.coding = c(s_system, s_code) as t_coding first then {
                    src.form_coding3_display as s_display where length() > 0 -> t_coding.display = s_display;
                };
            };
        };

        src.form_text as s_text where length() > 0 -> t_form.text = s_text;
    };

    // ingredient 0..*

    // ingredient.itemCodeableConcept 1..1
    src.ingredient_itemCodeableConcept_coding1_code as s_itemCoding1_code where length() > 0 -> tgt.ingredient as t_ingredient first then {
        
        src.ingredient_itemCodeableConcept_coding1_system as s_itemCoding1_system -> t_ingredient then {
            src.ingredient_itemCodeableConcept_coding1_display as s_itemCoding1_display -> t_ingredient.item = cc(s_itemCoding1_system, s_itemCoding1_code, s_itemCoding1_display) as t_item then {
                src.ingredient_itemCodeableConcept_coding2_code as s_coding_code where length() > 0 -> t_item then {
                    src.ingredient_itemCodeableConcept_coding2_system as s_coding_system -> t_item.coding = c(s_coding_system, s_coding_code) as t_coding then {
                        src.ingredient_itemCodeableConcept_coding2_display as s_coding_display where length() > 0 -> t_coding.display = s_coding_display;      
                    };
                };
                src.ingredient_itemCodeableConcept_text as s_text where length() > 0 -> t_item.text = s_text;
            };            
        };

        // ingredient.strength 0..1 Ratio
        src.ingredient_strength_numerator_value as s_value where length() > 0 -> t_ingredient.strength as t_strength then {
            s_value -> t_strength.numerator as t_numerator then {
                s_value -> t_numerator.value = s_value;
                src.ingredient_strength_numerator_unit as s_unit where length() > 0 -> t_numerator.unit = s_unit;
                src.ingredient_strength_numerator_code as s_code where length() > 0 -> t_numerator.code = s_code;
                src.ingredient_strength_numerator_system as s_system where length() > 0 -> t_numerator.system = s_system;
            };

            src.ingredient_strength_denominator_value as s_value where length() > 0 -> t_strength then {
                s_value -> t_strength.denominator as t_denominator then {
                    s_value -> t_denominator.value = s_value;
                    src.ingredient_strength_denominator_unit as s_unit where length() > 0 -> t_denominator.unit = s_unit;
                    src.ingredient_strength_denominator_code as s_code where length() > 0 -> t_denominator.code = s_code;
                    src.ingredient_strength_denominator_system as s_system where length() > 0 -> t_denominator.system = s_system;
                };
            };
        };
    };
}